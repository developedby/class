//Bits

//File for exercises in Bits

//e = empty
//o = 0
//i = 1
type Bits { 
  e
  o(pred: Bits)
  i(pred: Bits)
}

//a = Bits.i(Bits.i(Bits.e))
//b = Bits.o(Bits.i(Bits.i(Bits.e)))
//c = Bits.o(Bits.o(Bits.i(Bits.i(Bits.e))))
//d = Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))


//Verifies if it is an Empty Bit

//is_empty(a)      -> Bool.false
//is_empty(b)      -> Bool.false
//is_empty(c)      -> Bool.false
//is_empty(d)      -> Bool.false
//is_empty(Bits.e) -> Bool.true
is_empty(b: Bits): Bool
  case b {
    e: true
    o: false
    i: false
  }


//Gets the Tail of a Bits structure

//tail(a) -> Bits.i(Bits.e)
//tail(b) -> Bits.i(Bits.i(Bits.e))
//tail(c) -> Bits.o(Bits.i(Bits.i(Bits.e)))
//tail(d) -> Bits.i(Bits.i(Bits.i(Bits.e)))
tail(b: Bits): Bits
  case b {
    e: Bits.e
    o: b.pred
    i: b.pred
  }


//Negates all Bits

//notb(a) -> Bits.o(Bits.o(Bits.e))
//notb(b) -> Bits.i(Bits.o(Bits.o(Bits.e)))
//notb(c) -> Bits.i(Bits.i(Bits.o(Bits.o(Bits.e))))
//notb(d) -> Bits.o(Bits.o(Bits.o(Bits.o(Bits.e))))
notb(b: Bits): Bits
  case b {
    e: Bits.e
    o: Bits.i(notb(b.pred))
    i: Bits.o(notb(b.pred))
  }


//Increments a Bit

//inc(a) -> Bits.o(Bits.o(Bits.i(Bits.e)))
//inc(b) -> Bits.i(Bits.i(Bits.i(Bits.e)))
//inc(c) -> Bits.i(Bits.o(Bits.i(Bits.i(Bits.e))))
//inc(d) -> Bits.o(Bits.o(Bits.o(Bits.o(Bits.i(Bits.e)))))
inc(b: Bits): Bits
  case b {
    e: Bits.i(Bits.e)
    o: Bits.i(b.pred)
    i: Bits.o(inc(b.pred))
  }


//Decrements a Bit

//dec(a) -> Bits.o(Bits.i(Bits.e))
//dec(b) -> Bits.i(Bits.o(Bits.i(Bits.e)))
//dec(c) -> Bits.i(Bits.i(Bits.o(Bits.i(Bits.e))))
//dec(d) -> Bits.o(Bits.i(Bits.i(Bits.i(Bits.e))))
dec(b: Bits): Bits
  case b {
    e: Bits.e
    o: Bits.i(dec(b.pred))
    i: Bits.o(b.pred)
  }


//Concatenate two Bits structures

//concat(a,b) -> Bits.i(Bits.i(Bits.o(Bits.i(Bits.i(Bits.e)
//concat(b,c) -> Bits.o(Bits.i(Bits.i(Bits.o(Bits.o(Bits.i(Bits.i(Bits.e)))))))
//concat(c,d) -> Bits.o(Bits.o(Bits.i(Bits.i(Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))))))
concat(a: Bits, b: Bits): Bits
  case a {
    e: b
    o: Bits.o(concat(a.pred, b))
    i: Bits.i(concat(a.pred, b))
  }


//Adds two Bits structures

//add(a,b) -> Bits.i(Bits.o(Bits.o(Bits.i(Bits.e))))
//add(b,c) -> Bits.o(Bits.i(Bits.o(Bits.o(Bits.i(Bits.e)))))
//add(c,d) -> Bits.i(Bits.i(Bits.o(Bits.i(Bits.i(Bits.e)))))
add(a: Bits, b: Bits): Bits 
  case a {
    e: b
    o: case b {
      e: a
      o: Bits.o(add(a.pred, b.pred))
      i: Bits.i(add(a.pred, b.pred))
    }
    i: case b {
      e: a
      o: Bits.i(add(a.pred, b.pred))
      i: Bits.o(inc(add(a.pred, b.pred)))
    }
  }


//Returns Bits.i if only both bits are Bits.i

//and(a,b) -> Bits.o(Bits.i(Bits.e))
//and(b,c) -> Bits.o(Bits.o(Bits.i(Bits.e)))
//and(c,d) -> Bits.o(Bits.o(Bits.i(Bits.i(Bits.e))))
and(a: Bits, b: Bits): Bits
  case a b {
    o o: Bits.o(and(a.pred, b.pred))
    o i: Bits.o(and(a.pred, b.pred))
    i o: Bits.o(and(a.pred, b.pred))
    i i: Bits.i(and(a.pred, b.pred))
  } default Bits.e


//Returns Bits.i if any of bits are Bits.i

//or(a,b) -> Bits.i(Bits.i(Bits.i(Bits.e)))
//or(b,c) -> Bits.o(Bits.i(Bits.i(Bits.i(Bits.e))))
//or(c,d) -> Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))
or(a: Bits, b: Bits): Bits
  case a b {
    e e: Bits.e
    e o: Bits.o(or(Bits.e, b.pred))
    e i: Bits.i(or(Bits.e, b.pred))
    o e: Bits.o(or(a.pred, Bits.e))
    o o: Bits.o(or(a.pred, b.pred))
    o i: Bits.i(or(a.pred, b.pred))
    i e: Bits.i(or(a.pred, Bits.e))
    i o: Bits.i(or(a.pred, b.pred))
    i i: Bits.i(or(a.pred, b.pred))
  }


//Verifies if both Bit structures are equal

//eql(a,b) -> Bool.false
//eql(b,c) -> Bool.false
//eql(d,d) -> Bool.true
eql(a: Bits, b: Bits): Bool
  case a b {
    e e: true
    o o: eql(a.pred, b.pred)
    i i: eql(a.pred, b.pred)
  } default false


//Reverses a Bits structure

reverse_go(b: Bits, acum: Bits): Bits
  case b {
    e: acum
    o: reverse_go(b.pred, Bits.o(acum))
    i: reverse_go(b.pred, Bits.i(acum))
  }

//reverse(a) -> Bits.i(Bits.i(Bits.e))
//reverse(b) -> Bits.i(Bits.i(Bits.o(Bits.e)))
//reverse(c) -> Bits.i(Bits.i(Bits.o(Bits.o(Bits.e))))
//reverse(d) -> Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))
reverse(b: Bits): Bits
  reverse_go(b, Bits.e)


//Gets the length of the Bits structure

length_go(bs: Bits, acum: Nat): Nat
  case bs {
    e: acum
    o: length_go(bs.pred, Nat.succ(acum))
    i: length_go(bs.pred, Nat.succ(acum))
  }

//length(a) -> Nat.succ(Nat.succ(Nat.zero))
//length(b) -> Nat.succ(Nat.succ(Nat.succ(Nat.zero)))
//length(c) -> Nat.succ(Nat.succ(Nat.succ(Nat.succ(Nat.zero))))
//length(d) -> Nat.succ(Nat.succ(Nat.succ(Nat.succ(Nat.zero))))
length(bs: Bits): Nat
  length_go(bs, 0)


//Multiplies two Bits structures

mul_go(a: Bits, b: Bits, acum: Bits): Bits
  case a {
    e: acum
    o: mul_go(a.pred, Bits.o(b), acum)
    i: mul_go(a.pred, Bits.o(b), add(b, acum))
  }

//mul(a,b) -> Bits.o(Bits.i(Bits.o(Bits.o(Bits.i(Bits.e)))))
//mul(b,c) -> Bits.o(Bits.o(Bits.o(Bits.i(Bits.o(Bits.o(Bits.i(Bits.e)))))))
mul(a: Bits, b: Bits): Bits
  mul_go(a, b, Bits.e)


//Drops a n Number of Bits starting from from the least significative bit

//drop(1, a) -> Bits.i(Bits.e)
//drop(1, b) -> Bits.i(Bits.i(Bits.e))
//drop(1, c) -> Bits.o(Bits.i(Bits.i(Bits.e)))
//drop(1, d) -> Bits.i(Bits.i(Bits.i(Bits.e)))
drop(n: Nat, bs: Bits): Bits
  case n {
    zero: bs
    succ: case bs {
      e: Bits.e
      o: drop(n.pred, bs.pred)
      i: drop(n.pred, bs.pred)
    }
  }


//Takes n Bits from the structure, starting from the least significative bit

//take(1, a) -> Bits.i(Bits.e)
//take(2, b) -> Bits.o(Bits.i(Bits.e))
//take(3, c) -> Bits.o(Bits.o(Bits.i(Bits.e)))
//take(4, d) -> Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))
take(n: Nat, bs: Bits): Bits
  case n {
    zero: Bits.e
    succ: case bs {
      e: Bits.e
      o: Bits.o(take(n.pred, bs.pred))
      i: Bits.i(take(n.pred, bs.pred))
    }
  }


ex_07: _
  IO {
    let a = Bits.i(Bits.i(Bits.e))  // 3
    let b = Bits.o(Bits.i(Bits.i(Bits.e)))  // 6
    let c = Bits.o(Bits.o(Bits.i(Bits.i(Bits.e))))  // 12
    let d = Bits.i(Bits.i(Bits.i(Bits.i(Bits.e))))  // 15

    IO.print("is_empty")
    IO.print(Bool.show(is_empty(Bits.e)))
    IO.print(Bool.show(is_empty(Bits.o(Bits.e))))
    IO.print(Bool.show(is_empty(a)))
    IO.print(Bool.show(is_empty(b)))
    IO.print(Bool.show(is_empty(c)))
    IO.print(Bool.show(is_empty(d)))

    IO.print("tail")
    IO.print(Bits.show(tail(a)))
    IO.print(Bits.show(tail(b)))
    IO.print(Bits.show(tail(c)))
    IO.print(Bits.show(tail(d)))

    IO.print("complement")
    IO.print(Bits.show(complement(a)))
    IO.print(Bits.show(complement(b)))
    IO.print(Bits.show(complement(c)))
    IO.print(Bits.show(complement(d)))

    IO.print("inc")
    IO.print(Bits.show(inc(a)))
    IO.print(Bits.show(inc(b)))
    IO.print(Bits.show(inc(c)))
    IO.print(Bits.show(inc(d)))

    IO.print("dec")
    IO.print(Bits.show(dec(a)))
    IO.print(Bits.show(dec(b)))
    IO.print(Bits.show(dec(c)))
    IO.print(Bits.show(dec(d)))

    IO.print("concat")
    IO.print(Bits.show(concat(a,b)))
    IO.print(Bits.show(concat(b,c)))
    IO.print(Bits.show(concat(c,d)))

    IO.print("add")
    IO.print(Bits.show(add(a,b)))
    IO.print(Bits.show(add(b,c)))
    IO.print(Bits.show(add(c,d)))

    IO.print("and")
    IO.print(Bits.show(and(a,b)))
    IO.print(Bits.show(and(b,c)))
    IO.print(Bits.show(and(c,d)))

    IO.print("or")
    IO.print(Bits.show(or(a,b)))
    IO.print(Bits.show(or(b,c)))
    IO.print(Bits.show(or(c,d)))

    IO.print("eql")
    IO.print(Bool.show(eql(a,b)))
    IO.print(Bool.show(eql(b,c)))
    IO.print(Bool.show(eql(d,d)))

    IO.print("reverse")
    IO.print(Bits.show(reverse(a)))
    IO.print(Bits.show(reverse(b)))
    IO.print(Bits.show(reverse(c)))
    IO.print(Bits.show(reverse(d)))

    IO.print("length")
    IO.print(Nat.show(length(a)))
    IO.print(Nat.show(length(b)))
    IO.print(Nat.show(length(c)))
    IO.print(Nat.show(length(d)))

    IO.print("mul")
    IO.print(Bits.show(mul(a,b)))
    IO.print(Bits.show(mul(b,c)))
    IO.print(Bits.show(mul(c,d)))

    IO.print("drop")
    IO.print(Bits.show(drop(1,a)))
    IO.print(Bits.show(drop(1,b)))
    IO.print(Bits.show(drop(1,c)))
    IO.print(Bits.show(drop(2,d)))
    IO.print(Bits.show(drop(10,d)))

    IO.print("take")
    IO.print(Bits.show(take(1,a)))
    IO.print(Bits.show(take(2,b)))
    IO.print(Bits.show(take(3,c)))
    IO.print(Bits.show(take(4,d)))
    IO.print(Bits.show(take(20,d)))
  }